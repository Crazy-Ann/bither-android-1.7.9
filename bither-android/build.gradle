apply plugin: 'com.android.application'
android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 27
    buildToolsVersion '26.0.3'
    defaultConfig {
        applicationId "net.bither"
        targetSdkVersion 27
        minSdkVersion 19
        multiDexEnabled true
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
//            multiDexEnabled true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['native-libs']
            jni.srcDirs = []
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'org/apache/http/entity/mime/version.properties'
        exclude 'org/apache/http/version.properties'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
    }
    lintOptions {
        abortOnError false
        disable "ResourceType"
    }
    dexOptions {
        preDexLibraries = false
        additionalParameters = ['--core-library']
    }
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}
dependencies {
    compile 'com.google.protobuf:protobuf-java:2.5.0'
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'com.nineoldandroids:library:2.4.+'
    compile 'com.google.zxing:core:3.1.0@jar'
    compile 'com.android.support:multidex:1.0.0'
    compile 'org.json:json:20140107@jar'
    compile 'com.google.guava:guava:13.0.1@jar'
    compile 'com.google.code.findbugs:jsr305:2.0.1@jar'

    compile 'com.lambdaworks:scrypt:1.4.0@jar'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.madgag.spongycastle:core:1.51.0.0'

    compile 'org.apache.httpcomponents:httpclient:4.2.1@jar'
    compile 'org.apache.httpcomponents:httpmime:4.1@jar'
    compile 'org.apache.httpcomponents:httpcore:4.3.2@jar'
    compile 'org.apache.james:apache-mime4j-core:0.7.1@jar'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'org.json:json:20140107@jar'
    testCompile 'com.google.guava:guava:13.0.1@jar'
    testCompile 'com.google.code.findbugs:jsr305:2.0.1@jar'

    compile ('com.github.tony19:logback-android-classic:1.0.10-2@jar'){
        // Work around for gradle 0.9 and 0.10
        transitive = true
    }
}